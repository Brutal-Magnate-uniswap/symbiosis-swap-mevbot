#!/usr/bin/perl
#
#  Report on any missing package dependencies.
#
# Steve
# ---
#




#
#  Holder for the dependencies.
#
my %depends;


#
#  Process every package.
#
foreach my $file ( sort( glob( "*/debian/control" ) ) )
{
    processFile( $file );
}


#
#  Now our hardwired list of packages
#
foreach my $required ( qw! dpkg-dev devscripts build-essential make ! )
{
    if ( !packageInstalled( $required ) )
    {
        $depends{$required} += 1;
    }
}

#
#  Are we OK?
#
my $count = scalar keys( %depends );
if ( ! $count )
{
    exit 0;
}


#
#  Otherwise show the packages which are missing.
#
print "The following packages are missing:\n";
foreach my $package ( keys %depends )
{
    if ( -d "/home/vhostbld" )
    {
        system( "apt-get install -y --force-yes $package" );
    }
    else
    {
        print "\t" . $package . "\n";
    }
}
exit 1;




=begin doc

  Look for build-deps.

=end doc

=cut

sub processFile
{
    my( $file ) = ( @_ );

    open( FILE, "<", $file )
    or die "Failed to open $file - $!";
    while( <FILE> )
    {
        if ( $_ =~ /Build-Depends: (.*)/ )
        {
            my $deps = $1;
            foreach my $entry ( split( /,/, $deps ) )
            {
                if ( $entry =~ /(.*)\(.*\)/ )
                {
                    $entry = $1;
                }
                $entry =~ s/^\s+|\s+$//g;

                $entry = "apache2-threaded-dev" if ( $entry =~ /apache2-dev/ );

                #
                #  Record the entry.
                #
                if ( !packageInstalled( $entry ) )
                {
                    $depends{$entry} += 1;
                }
            }
        }
    }
    close( FILE );

}


=begin doc

  Return 1 if the named package is installed.

=end doc

=cut

sub packageInstalled
{
    my( $package ) = ( @_ );

    my $lines = `dpkg --list $package | grep ^ii`;

    if ( ( $lines ) && ( $lines =~ /\Q$package\E/ ) )
    {
        return 1;
    }
    return 0;

}
